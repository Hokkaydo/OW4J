plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id 'io.codearte.nexus-staging' version '0.11.0'
    id 'maven-publish'
    id 'signing'
}

allprojects {
    group 'tech.hokkaydo'
    version '0.1.1-alpha'
    archivesBaseName = "ow4j"

    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    apply plugin: "com.github.johnrengelman.shadow"

    compileJava.options.encoding = 'UTF-8'

    sourceCompatibility = 1.8
}

subprojects {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
        compile 'org.jsoup:jsoup:1.13.1'
    }
    test {
        useJUnitPlatform()
    }
}


// Signing
apply plugin: 'signing'
signing {
    sign configurations.archives
}


// Deploying
apply plugin: 'maven'

// Add Javadoc JAR and sources JAR to artifact
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
artifacts {
    archives javadocJar, sourcesJar
}

// Build, sign, and upload
uploadArchives {
    repositories {
        mavenDeployer {

            if (!hasProperty('nexusUsername')) {
                ext.nexusUsername = ''
            }
            if (!hasProperty('nexusPassword')) {
                ext.nexusPassword = ''
            }

            // Sign POM
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            // Destination
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: nexusUsername ?:'', password: nexusPassword ?: '')
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: nexusUsername ?:'', password: nexusPassword ?: '')
            }

            // Add required metadata to POM
            pom.project {
                name 'OW4J'
                packaging 'jar'
                description 'Overwatch Java API'
                url 'https://github.com/Hokkaydo/OW4J'

                scm {
                    connection 'scm:git:git://github.com/Hokkaydo/OW4J.git'
                    developerConnection 'scm:git:ssh://github.com/Hokkaydo/OW4J.git'
                    url 'https://github.com/Hokkaydo/OW4J/tree/master'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'Hokkaydo'
                        name 'Alexandre Or√©khoff'
                        email 'alexandre.orekhoff@gmail.com'
                        organization 'Hokkaydo'
                        organizationUrl 'https://hokkaydo.tech'
                    }
                }
            }
        }
    }
}

nexusStaging {
    packageGroup = "tech.hokkaydo"
}